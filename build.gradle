plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.github.spotbugs' version '6.0.7'
    id 'info.solidsoft.pitest' version '1.15.0'
}

group = 'app.fileconverter'
version = '1.0.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.cloudconvert:cloudconvert-java:1.2.3'
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'ch.qos.logback:logback-classic:1.4.11'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'com.tngtech.archunit:archunit-junit5:1.2.1'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    testImplementation 'org.pitest:pitest-junit5-plugin:1.2.1'
}

javafx {
    version = '21.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

application {
    // Use a plain Java entry point for packaged/classpath launches.
    // Launching an Application subclass directly can fail with:
    // "JavaFX runtime components are missing..."
    mainClass = 'app.ui.Launcher'
}

tasks.named('run', JavaExec) {
    def isMac = System.getProperty("os.name")
            .toLowerCase(java.util.Locale.ROOT)
            .contains("mac")
    if (isMac) {
        jvmArgs '-Dapple.awt.application.name=Converto', '-Xdock:name=Converto'
        def dockIcon = file("${rootDir}/src/main/resources/icons/app-icon.png")
        if (dockIcon.exists()) {
            jvmArgs "-Xdock:icon=${dockIcon.absolutePath}"
        }
    }
}

tasks.named('startScripts') {
    doLast {
        def javafxJars = classpath.files
                .findAll { it.name.startsWith('javafx-') }
                .collect { it.name }
                .sort()

        if (!javafxJars.isEmpty()) {
            def unixModulePath = javafxJars.collect { "\$APP_HOME/lib/${it}" }.join(':')
            def windowsModulePath = javafxJars.collect { "%APP_HOME%\\lib\\${it}" }.join(';')

            unixScript.text = unixScript.text.replace(
                    'set -- \\',
                    "set -- \\\n        --module-path \"${unixModulePath}\" \\\n        --add-modules javafx.controls,javafx.fxml \\"
            )

            windowsScript.text = windowsScript.text.replace(
                    '"%JAVA_EXE%" %DEFAULT_JVM_OPTS% %JAVA_OPTS% %FILE_CONVERTER_OPTS%  -classpath "%CLASSPATH%" app.ui.Launcher %*',
                    '"%JAVA_EXE%" %DEFAULT_JVM_OPTS% %JAVA_OPTS% %FILE_CONVERTER_OPTS%  --module-path "' +
                            windowsModulePath +
                            '" --add-modules javafx.controls,javafx.fxml -classpath "%CLASSPATH%" app.ui.Launcher %*'
            )
        }
    }
}

tasks.register('jpackageApp', Exec) {
    dependsOn installDist
    def distDir = layout.buildDirectory.get().asFile
    def libDir = new File(distDir, "install/file-converter/lib")
    def destDir = new File(distDir, "jpackage")
    def iconIco = file("${rootDir}/src/main/packaging/app-icon.ico")
    def iconIcns = file("${rootDir}/src/main/packaging/app-icon.icns")
    def iconPng = file("${rootDir}/src/main/packaging/app-icon.png")
    def osName = System.getProperty("os.name").toLowerCase(java.util.Locale.ROOT)
    def isMac = osName.contains("mac")
    def isWindows = osName.contains("win")
    doFirst {
        destDir.mkdirs()
        def existingAppImage = new File(destDir, "Converto.app")
        if (existingAppImage.exists()) {
            existingAppImage.deleteDir()
        }
        def appImageVersion = version.toString().replaceFirst(/-.*/, '')
        def args = [
                'jpackage',
                '--type', 'app-image',
                '--name', 'Converto',
                '--app-version', appImageVersion,
                '--input', libDir,
                '--main-jar', "file-converter-${version}.jar",
                '--main-class', 'app.ui.Launcher',
                '--dest', destDir
        ]
        if (isMac) {
            args += ['--mac-package-name', 'Converto']
        }
        def iconFile = null
        if (isMac) {
            iconFile = (iconIcns.exists() && iconIcns.length() > 0)
                    ? iconIcns
                    : ((iconPng.exists() && iconPng.length() > 0) ? iconPng : null)
        } else if (isWindows) {
            iconFile = (iconIco.exists() && iconIco.length() > 0) ? iconIco : null
        } else {
            iconFile = (iconPng.exists() && iconPng.length() > 0)
                    ? iconPng
                    : ((iconIco.exists() && iconIco.length() > 0) ? iconIco : null)
        }
        if (iconFile != null) {
            args += ['--icon', iconFile.absolutePath]
            logger.lifecycle("Using app icon: ${iconFile}")
        } else {
            logger.lifecycle("Custom icon not found; using default jpackage icon")
        }
        // Put JavaFX on module path to avoid "Unsupported JavaFX configuration" warning
        def javafxJars = libDir.listFiles()
                .findAll { it.name.startsWith('javafx-') }
                .collect { "\$APPDIR/${it.name}" }
                .join(':')
        if (javafxJars) {
            args += ['--java-options', "--module-path=${javafxJars}"]
            args += ['--java-options', '--add-modules=javafx.controls,javafx.fxml']
        }
        commandLine args
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

checkstyle {
    toolVersion = '10.12.5'
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    ignoreFailures = false
}

spotbugs {
    excludeFilter = file("${rootDir}/config/spotbugs/exclude.xml")
}

spotbugsMain {
    reports {
        html.required = true
        xml.required = true
    }
}

spotbugsTest {
    reports {
        html.required = true
        xml.required = true
    }
    ignoreFailures = true
}

pitest {
    targetClasses = ['app.core.*', 'app.persistence.*']
    targetTests = ['app.*']
    outputFormats = ['HTML', 'XML']
    mutationThreshold = 0
    threads = 4
}

tasks.named('check') {
    dependsOn spotbugsMain
}
